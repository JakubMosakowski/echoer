plugins {
    id 'com.android.application'
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "dagger.hilt.android.plugin"
    id 'de.mannodermaus.android-junit5'
}

android {
    compileSdkVersion compile_sdk

    defaultConfig {
        applicationId "com.jakmos.echoer"
        minSdk min_sdk
        targetSdk target_sdk
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    def keystorePropertiesFile = rootProject.file("settings/keystore/keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        debug {
            keyAlias = "androiddebugkey"
            keyPassword = "android"
            storePassword = "android"
            storeFile = rootProject.file("settings/keystore/debug.jks")
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
            storeFile rootProject.file("settings/keystore/release.jks")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".dev"
            versionNameSuffix " Development"
            minifyEnabled = false
            resValue "string", "app_name", "Echoer Development"
            signingConfig = signingConfigs.debug
            buildConfigField("String", "SERVER_LOGGING_LEVEL", "\"BODY\"")
            buildConfigField 'Boolean', 'ENABLE_CRASHLYTICS', 'false'
        }
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Echoer"
            signingConfig = signingConfigs.release
            buildConfigField("String", "SERVER_LOGGING_LEVEL", "\"NONE\"")
            buildConfigField 'Boolean', 'ENABLE_CRASHLYTICS', 'true'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // Core
    implementation "androidx.core:core-ktx:$android_core_version"

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.activity:activity-compose:$compose_activity_version"
    implementation "androidx.navigation:navigation-compose:$compose_nav_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebase_version")
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-work:$hilt_androidx_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_androidx_version"

    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutines_version"

    // Moshi
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Orbit
    implementation "org.orbit-mvi:orbit-viewmodel:$orbit_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_logging_interceptor_version"

    // Other
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "io.kotest:kotest-assertions-core:$kotest_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "androidx.arch.core:core-testing:$core_testing_version"
    testImplementation "app.cash.turbine:turbine:$turbine_version"
}
